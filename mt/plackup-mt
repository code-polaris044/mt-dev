#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long qw();
use Filesys::Notify::Simple;

my $server       = 'starman';
my @plackup_opts = ();

# Preload
push @plackup_opts, map {"-M$_"} qw(
    CGI
    File::Spec
    CGI::Cookie
    LWP::UserAgent
    HTML::Entities
    Scalar::Util
    DBI
    DBD::mysql
    Image::Magick
);

init_watcher(@ARGV);
exec $server, @plackup_opts, @ARGV;

sub init_watcher {
    my @argv = @_;

    my $p = Getopt::Long::Parser->new( config => [qw/pass_through/] );

    $p->getoptionsfromarray( \@argv, 'pid=s' => \my $pid_file, );

    if ( $pid_file && !fork() ) {
        my @files   = ( qw(addons extlib lib plugins), glob('*.cgi'), );
        my $watcher = Filesys::Notify::Simple->new( \@files );
        while (1) {
            $watcher->wait(
                sub {
                    my ($pid) = do { open( my $fh, '<', $pid_file ); <$fh> };
                    kill 'HUP', $pid;
                }
            );
        }
    }
}
